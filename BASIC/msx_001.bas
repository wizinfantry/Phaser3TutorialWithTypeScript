'■札戦闘遊戯『妖術』

'カードバトルゲームです。目的は自分以外の３人の体力を０にす'ることです。
'毎ターン、４枚の手札の中から１枚を選んでつかいます。札は自動で補充されます。

'★パラメータについて
'ライフ　：３０でスタート。上限は３５。
'パワー　：攻撃力。値が大きな程、威力が増えます。
'スピード：素早さ。相手より値が大きな程、攻撃回数が増えます。

'★札詳細（全１１種）
'ファイアー：一人に中程度のダメージ。
'コールド　：一人を凍らせて行動を停止させます。
'サンダー　：敵全員に弱程度のダメージ。
'クラッシュ：一人に強程度のダメージ。
'　　　　　　ただし、パワー・スピードを１ずつ消費します。
'ＰＯＷＥＲ：パワーを１増加させます。
'ＳＰＥＥＤ：スピードを１増加させます。
'バリア　　：すべての攻撃を防ぐことができます。
'ベホイミ　：ライフを回復します。
'　　　　　　パワー・スピードの値が大きな程、回復量が増えます。
'（＾＿＾）：全員のパワーを１にしてしまいます。
'（＾ｏ＾）：全員のスピードを１にしてしまいます。
'チェンジ　：手札を４枚すべて変えます。

'平等性と戦略性をつくるために、一人に対する攻撃は順番に当たります。
'サバイバルモードをつけたので、何回勝ち抜けるか挑戦してみて下さい。

30 ' Card Battle Game 「MAGE」
31 ' made by Rurun
32 ' E-mail:yoshiro@athena.dricas.com
33 ' Thanks to
40 '   ｲｶｶﾞﾜｼｨｰｸﾚｯﾄｻｰﾋﾞｽ
50 '   Nasubi Fujitaka
60 '   Moemi Jinno
70 '

71  @PAD_A     = &H400
72  @PAD_B     = &H100
73  @PAD_C     = &H200
74  @PAD_X    = &H40
75  @PAD_Y     = &H20
76  @PAD_Z     = &H10
77  @PAD_R     = &H80
78  @PAD_L     = &H8
79  @PAD_RIGHT = &H8000
80  @PAD_LEFT  = &H4000
81  @PAD_DOWN  = &H2000
82  @PAD_UP    = &H1000
83  @PAD_START = &H800

100 SCREEN 0:KEY OFF
110 DIM HP(4),C(7),P(4),S(4),FZ(4),V(4)
120 FOR L=0 TO 3
130 HP(L)=30:P(L)=1:S(L)=1:FZ(L)=0
140 NEXT L
150 MATX=INT(RND(1)*4):X=1:REM MATX=RND(4):X=1
160 FOR L=0 TO 6:C(L)=INT(RND(1)*25):NEXT L:REM FOR L=0 TO 6:C(L)=RND(25):NEXT L
170 REM LINE (0,0)-(319,223),RGB(0,0,18),BF
180 REM *LOOP
190 W=1:FZ(0)=FZ(0)-1:CLS
200 IF X>1 THEN LOCATE 25,4:PRINT "Survival";X
210 LOCATE 4,1:PRINT "LIFE/POWER/SPEED"
220 IF HP(1)<1 THEN 230 ELSE 250:REM IF HP(1)<1 THEN 230
230  LOCATE 1,3:PRINT " A - DEAD"
240  GOTO 280
250 REM ENDIF
260 LOCATE 1,3:PRINT USING " A - ## / # / #";HP(1);P(1);S(1)
270 PLAY "C":REM EFFECT "@09 C"
280 IF HP(2)<1 THEN 290 ELSE 310:REM IF HP(2)<1 THEN
290  LOCATE 1,5:PRINT " B - DEAD"
300  GOTO 340
310 REM ENDIF
320 LOCATE 1,5:PRINT USING " B - ## / # / #";HP(2);P(2);S(2)
330 PLAY "C":REM EFFECT "@09 C":
340 IF HP(3)<1 THEN 350 ELSE 370:REM IF HP(3)<1 THEN
350  LOCATE 1,7:PRINT " C - DEAD"
360  GOTO 400
370 REM ENDIF
380 LOCATE 1,7:PRINT USING " C - ## / # / #";HP(3);P(3);S(3)
390 PLAY "C":REM EFFECT "@09 C"
400 IF HP(0)<1 THEN 410 ELSE 430:REM IF HP(0)<1 THEN
410  LOCATE 1,9:PRINT "YOU- DEAD"
420  GOTO 3010:REM GOTO *LOOSE
430 REM ENDIF
440 LOCATE 1,9:PRINT USING "YOU- ## / # / #";HP(0);P(0);S(0)
450 PLAY "C":REM EFFECT "@09 C"
460 IF HP(1)<1 AND HP(2)<1 THEN 470 ELSE 480:REM IF HP(1)<1 AND HP(2)<1 THEN
470  IF HP(3)<1 AND HP(0)>0 THEN GOTO 3170:REM IF HP(3)<1 AND HP(0)>0 THEN GOTO *WIN
480 REM ENDIF
490 IF FZ(0)>0 THEN 500 ELSE 540:REM IF FZ(0)>0 THEN
500  LOCATE 2,13:PRINT "YOU freezing."
510  LOCATE 4,15:PRINT "PUSH C"
520  A$=INKEY$:IF A$=CHR$(67) OR A$=CHR$(99) THEN 880:REM IF PAD(0,1) AND @PAD_C THEN GOTO 880
530  GOTO 520
540 REM ENDIF
550 F=C(0):GOSUB 1260:REM GOSUB *CARD
560 LOCATE 2,13:PRINT G$
570 F=C(4):GOSUB 1260:REM GOSUB *CARD
580 LOCATE 9,13:PRINT G$
590 F=C(5):GOSUB 1260:REM GOSUB *CARD
600 LOCATE 16,13:PRINT G$
610 F=C(6):GOSUB 1260:REM GOSUB *CARD
620 LOCATE 23,13:PRINT G$
630 LOCATE 1,17:PRINT "Select your card, and PUSH C"
640 LOCATE 1,12:PRINT "======="
650 LOCATE 1,14:PRINT "======="
660 A$=INKEY$:IF A$=CHR$(28) THEN 670 ELSE 700:REM IF PAD(0,1) AND @PAD_RIGHT THEN
670  W=W+1:REM VSYNC 10:W=W+1
680  IF W=5 THEN W=1
690  GOTO 780
700 REM ENDIF
710 IF A$=CHR$(29) THEN 720 ELSE 750:REM IF PAD(0,1) AND @PAD_LEFT THEN
720  W=W-1:REM VSYNC 10:W=W-1
730  IF W=0 THEN W=4
740  GOTO 780
750 REM ENDIF
760 IF A$=CHR$(67) OR A$=CHR$(99) THEN 840:REM IF PAD(0,1) AND @PAD_C THEN GOTO 840
770 GOTO 660
780 LOCATE 1,12:PRINT "                              "
790 LOCATE 1,14:PRINT "                              "
800 IF W=1 GOTO 640
810 IF W=2 THEN LOCATE 8,12:PRINT "=======":LOCATE 8,14:PRINT "=======":GOTO 660
820 IF W=3 THEN LOCATE 15,12:PRINT "=======":LOCATE 15,14:PRINT "=======":GOTO 660
830 IF W=4 THEN LOCATE 22,12:PRINT "=======":LOCATE 22,14:PRINT "=======":GOTO 660
840 IF W=1 THEN V(0)=C(0):C(0)=INT(RND(1)*25):REM C(0)=RND(25)
850 IF W=2 THEN V(0)=C(4):C(4)=INT(RND(1)*25):REM C(4)=RND(25)
860 IF W=3 THEN V(0)=C(5):C(5)=INT(RND(1)*25):REM C(5)=RND(25)
870 IF W=4 THEN V(0)=C(6):C(6)=INT(RND(1)*25):REM C(6)=RND(25)
880 FOR L=1 TO 3
890 V(L)=C(L):C(L)=INT(RND(1)*25)
900 IF FZ(L)>1 THEN V(L)=0
910 NEXT L
920 FZ(0)=FZ(0)+1:CLS
930 TURN=-1
940 TURN=TURN+1
950 IF TURN=4 GOTO 1050
960 FZ(TURN)=FZ(TURN)-1
970 GOSUB 1080:REM GOSUB *NAME
980 IF HP(TURN)<1 GOTO 940
990 IF FZ(TURN)>0 THEN LOCATE 3,25:PRINT WHO$;" freezing.":GOTO 1030
1000 F=V(TURN):GOSUB 1260:REM GOSUB *CARD
1010 LOCATE 3,25:PRINT WHO$;" used ";G$
1020 GOSUB 1530:REM GOSUB *JUDGE
1030 LOCATE 0,25:PRINT :GOSUB 4000:REM VSYNC 30
1040 GOTO 940
1050 LOCATE 5,25:PRINT "PUSH A"
1060 A$=INKEY$:IF A$=CHR$(65) OR A$=CHR$(97) THEN 180:REM IF PAD(0,1) AND @PAD_A THEN GOTO 180:REM GOTO *LOOP
1070 GOTO 1060
1080 REM *NAME
1090 IF TURN=0 THEN WHO$="YOU"
1100 IF TURN=1 THEN WHO$=" A "
1110 IF TURN=2 THEN WHO$=" B "
1120 IF TURN=3 THEN WHO$=" C "
1130 RETURN
1140 REM *NAME2
1150 IF MATX=0 THEN WHO$="YOU"
1160 IF MATX=1 THEN WHO$=" A "
1170 IF MATX=2 THEN WHO$=" B "
1180 IF MATX=3 THEN WHO$=" C "
1190 RETURN
1200 REM *NAME3
1210 IF L=0 THEN WHO$="YOU"
1220 IF L=1 THEN WHO$=" A "
1230 IF L=2 THEN WHO$=" B "
1240 IF L=3 THEN WHO$=" C "
1250 RETURN
1260 REM *CARD
1270 IF F=0 THEN G$="FIRE"
1280 IF F=1 THEN G$="FIRE"
1290 IF F=2 THEN G$="FIRE"
1300 IF F=3 THEN G$="COLD"
1310 IF F=4 THEN G$="COLD"
1320 IF F=5 THEN G$="COLD"
1330 IF F=6 THEN G$="BOLT"
1340 IF F=7 THEN G$="BOLT"
1350 IF F=8 THEN G$="BOLT"
1360 IF F=9 THEN G$="CRASH"
1370 IF F=10 THEN G$="CRASH"
1380 IF F=11 THEN G$="POWER"
1390 IF F=12 THEN G$="POWER"
1400 IF F=13 THEN G$="POWER"
1410 IF F=14 THEN G$="SPEED"
1420 IF F=15 THEN G$="SPEED"
1430 IF F=16 THEN G$="SPEED"
1440 IF F=17 THEN G$="BARIA"
1450 IF F=18 THEN G$="BARIA"
1460 IF F=19 THEN G$="LIFEUP"
1470 IF F=20 THEN G$="LIFEUP"
1480 IF F=21 THEN G$="(^_^)"
1490 IF F=22 THEN G$="(^o^)"
1500 IF F=23 THEN G$="CHANGE"
1510 IF F=24 THEN G$="CHANGE"
1520 RETURN
1530 REM *JUDGE
1540 IF F<3 GOTO 1730:REM IF F<3 GOTO *FIRE
1550 IF F<6 GOTO 1920:REM IF F<6 GOTO *COLD
1560 IF F<9 GOTO 2070:REM IF F<9 GOTO *THUNDER
1570 IF F<11 GOTO 2280:REM IF F<11 GOTO *CRASH
1580 IF F<14 GOTO 2500:REM IF F<14 GOTO *POWER
1590 IF F<17 GOTO 2540:REM IF F<17 GOTO *SPEED
1600 IF F<19 GOTO 2850:REM IF F<19 GOTO *BARIA
1610 IF F<21 GOTO 2880:REM IF F<21 GOTO *BEHOIMI
1620 IF F<23 GOTO 2580:REM IF F<23 GOTO *KAO
1630 GOTO 2950:REM GOTO *CHANGE
1640 REM *HIT
1650 FOR L=1 TO 3
1660 IF HP(MATX)<1 THEN MATX=MATX+1
1670 IF MATX=4 THEN MATX=0
1680 IF TURN=MATX THEN MATX=MATX+1
1690 IF MATX=4 THEN MATX=0
1700 NEXT L
1710 GOSUB 1140:REM GOSUB *NAME2
1720 RETURN
1730 REM *FIRE
1740 PLAY "F":GOSUB 4000:REM EFFECT "@14 F":VSYNC 30
1750 GOSUB 1640:REM GOSUB *HIT
1760 IF V(MATX)<17 OR V(MATX)>18 THEN 1770 ELSE 1840:REM IF V(MATX)<17 OR V(MATX)>18 THEN
1770  LL=S(TURN)-S(MATX)+1
1780  IF LL<1 THEN LL=1
1790  FOR L=1 TO LL
1800  LOCATE 5,25:PRINT WHO$;" damaged";2+P(TURN)
1810  HP(MATX)=HP(MATX)-2-P(TURN)
1820  PLAY "C":GOSUB 4000:REM EFFECT "@15 C":VSYNC 30
1830  NEXT L
1840 REM ENDIF
1850 IF V(MATX)=17 OR V(MATX)=18 THEN 1860 ELSE 1880:REM IF V(MATX)=17 OR V(MATX)=18 THEN
1860  LOCATE 5,25:PRINT WHO$;"BARRIER!!"
1870  PLAY "C":GOSUB 4000:REM EFFECT "@27 C":VSYNC 30
1880 REM ENDIF
1890 MATX=MATX+1
1900 IF MATX=4 THEN MATX=0
1910 RETURN
1920 REM *COLD
1930 PLAY "F":GOSUB 4000:REM EFFECT "@13 F":VSYNC 30
1940 GOSUB 1640:REM GOSUB *HIT
1950 IF V(MATX)<17 OR V(MATX)>18 THEN 1960 ELSE 1990:REM IF V(MATX)<17 OR V(MATX)>18 THEN
1960  LOCATE 5,25:PRINT WHO$;" freeze";P(TURN)
1970  FZ(MATX)=P(TURN)+1
1980  PLAY "F":GOSUB 4000:REM EFFECT "@11 F":VSYNC 30
1990 REM ENDIF
2000 IF V(MATX)=17 OR V(MATX)=18 THEN 2010 ELSE 2030:REM IF V(MATX)=17 OR V(MATX)=18 THEN
2010  LOCATE 5,25:PRINT WHO$;"BARRIER!!"
2020  PLAY "C":GOSUB 4000:REM EFFECT "@27 C":VSYNC 30
2030 REM ENDIF
2040 MATX=MATX+1
2050 IF MATX=4 THEN MATX=0
2060 RETURN
2070 REM *THUNDER
2080 PLAY "C":L=-1:GOSUB 4000:REM EFFECT "@12 C":VSYNC 30:L=-1
2090 L=L+1
2100 IF L=4 THEN RETURN
2110 GOSUB 1200:REM GOSUB *NAME3
2120 IF HP(L)<1 THEN GOTO 2090
2130 IF V(L)=17 OR V(L)=18 THEN 2140 ELSE 2160:REM IF V(L)=17 OR V(L)=18 THEN
2140  LOCATE 5,25:PRINT WHO$;"BARRIER!!"
2150  PLAY "C":GOSUB 4000:REM EFFECT "@27 C":VSYNC 30
2160 REM ENDIF
2170 IF V(L)<17 OR V(L)>18 THEN 2180 ELSE 2260:REM IF V(L)<17 OR V(L)>18 THEN
2180  IF TURN=L THEN GOTO 2090
2190  LL=S(TURN)-S(L)+1
2200  IF LL<1 THEN LL=1
2210  FOR K=1 TO LL
2220  LOCATE 5,25:PRINT WHO$;" damaged";P(TURN)
2230  HP(L)=HP(L)-P(TURN)
2240  PLAY "C":GOSUB 4000:REM EFFECT "@15 C":VSYNC 30
2250  NEXT K
2260 REM ENDIF
2270 GOTO 2090
2280 REM *CRASH
2290 GOSUB 4000:REM EFFECT "@06 < B12 @08 A":VSYNC 30
2300 GOSUB 1640:REM GOSUB *HIT
2310 IF V(MATX)<17 OR V(MATX)>18 THEN 2320 ELSE 2390:REM IF V(MATX)<17 OR V(MATX)>18 THEN
2320  LL=S(TURN)-S(MATX)+2
2330  IF LL<1 THEN LL=1
2340  FOR L=1 TO LL
2350  LOCATE 5,25:PRINT WHO$;" damaged";P(TURN)+S(TURN)
2360  HP(MATX)=HP(MATX)-P(TURN)-S(TURN)
2370  PLAY "C":GOSUB 4000:REM EFFECT "@15 C":VSYNC 30
2380  NEXT L
2390 REM ENDIF
2400 IF V(MATX)=17 OR V(MATX)=18 THEN 2410 ELSE 2430:REM IF V(MATX)=17 OR V(MATX)=18 THEN
2410  LOCATE 5,25:PRINT WHO$;"BARRIER!!"
2420  PLAY "C":GOSUB 4000:REM EFFECT "@27 C":VSYNC 30
2430 REM ENDIF
2440 P(TURN)=P(TURN)-1:S(TURN)=S(TURN)-1
2450 IF P(TURN)<1 THEN P(TURN)=1
2460 IF S(TURN)<1 THEN S(TURN)=1
2470 MATX=MATX+1
2480 IF MATX=4 THEN MATX=0
2490 RETURN
2500 REM *POWER
2510 PLAY "C":GOSUB 4000:REM EFFECT "@22 C":VSYNC 30
2520 P(TURN)=P(TURN)+1:IF P(TURN)>9 THEN P(TURN)=9
2530 RETURN
2540 REM *SPEED
2550 PLAY "C":GOSUB 4000:REM EFFECT "@22 C":VSYNC 30
2560 S(TURN)=S(TURN)+1:IF S(TURN)>9 THEN S(TURN)=9
2570 RETURN
2580 REM *KAO
2590 PLAY "G":GOSUB 4000:REM EFFECT "@23 < G":VSYNC 30
2600 IF V(TURN)=21 THEN 2610 ELSE 2710:REM IF V(TURN)=21 THEN
2610  LOCATE 5,25:PRINT "all power down."
2620  FOR L=0 TO 3
2630  GOSUB 1200:REM GOSUB *NAME3
2640  IF V(L)<17 OR V(L)>18 THEN P(L)=1
2650  IF V(L)=17 OR V(L)=18 THEN 2660 ELSE 2690:REM IF V(L)=17 OR V(L)=18 THEN
2660   IF HP(L)>0 THEN 2670 ELSE 2690:REM IF HP(L)>0 THEN
2670    LOCATE 5,25:PRINT WHO$;"BARRIER!!"
2680    PLAY "C":GOSUB 4000:REM EFFECT "@27 C":VSYNC 30
2690  REM ENDIF :REM ENDIF
2700  NEXT L
2710 REM ENDIF
2720 IF V(TURN)=22 THEN 2730 ELSE 2830:REM IF V(TURN)=22 THEN
2730  LOCATE 5,25:PRINT "all speed down."
2740  FOR L=0 TO 3
2750  GOSUB 1200:REM GOSUB *NAME3
2760  IF V(L)<17 OR V(L)>18 THEN S(L)=1
2770  IF V(L)=17 OR V(L)=18 THEN 2780 ELSE 2810:REM IF V(L)=17 OR V(L)=18 THEN
2780   IF HP(L)>0 THEN 2790 ELSE 2810:REM IF HP(L)>0 THEN
2790    LOCATE 5,25:PRINT WHO$;"BARRIER!!"
2800    PLAY "C":GOSUB 4000:REM EFFECT "@27 C":VSYNC 30
2810  REM ENDIF :REM ENDIF
2820  NEXT L
2830 REM ENDIF
2840 RETURN
2850 REM *BARIA
2860 PLAY "C":GOSUB 4000:REM EFFECT "@27 C":VSYNC 30
2870 RETURN
2880 REM *BEHOIMI
2890 PLAY "C":GOSUB 4000:REM EFFECT "@30 C":VSYNC 30
2900 LOCATE 5,25:PRINT WHO$;" Life up";P(TURN)+S(TURN)+1
2910 HP(TURN)=HP(TURN)+P(TURN)+S(TURN)+1
2920 IF HP(TURN)>35 THEN HP(TURN)=35
2930 PLAY "C":GOSUB 4000:REM EFFECT "@29 C16":VSYNC 30
2940 RETURN
2950 REM *CHANGE
2960 PLAY "C":GOSUB 4000:REM EFFECT "@09 C":VSYNC 30
2970 IF TURN<>0 THEN RETURN
2980 LOCATE 5,25:PRINT "Your card is changed."
2990 FOR L=0 TO 6:C(L)=INT(RND(1)*25):NEXT L:REM C(L)=RND(25):NEXT L
3000 RETURN
3010 REM *LOOSE
3020 X=1:GOSUB 4000:REM VSYNC 30:X=1
3030 LOCATE 1,17:PRINT "RETRY A / END C"
3040 IF X>1 THEN LOCATE 11,17:PRINT "SURVIVAL B / END C"
3050 A$=INKEY$:IF A$=CHR$(65) OR A$=CHR$(97) THEN 120:REM IF PAD(0,1) AND @PAD_A THEN GOTO 120
3060 IF A$=CHR$(66) OR A$=CHR$(98) THEN 3070 ELSE 3140
3070  IF X>1 THEN 3080 ELSE 3140
3080   FOR L=1 TO 3
3090   HP(L)=30:P(L)=1:S(L)=1:FZ(L)=0:C(L+3)=INT(RND(1)*25):REM C(L+3)=RND(25)
3100   NEXT L
3110   MATX=INT(RND(1)*3):C(0)=INT(RND(1)*25):REM MATX=RND(3)+1:C(0)=RND(25)
3120   IF HP(0)<20 THEN HP(0)=HP(0)+P(0)+S(0)+1:PLAY "C":REM EFFECT "@30 C"
3130   GOTO 180:REM GOTO *LOOP
3140 REM ENDIF :REM ENDIF
3150 IF A$=CHR$(67) OR A$=CHR$(99) THEN END:REM IF PAD(0,1) AND @PAD_C THEN END
3160 GOTO 3050
3170 REM *WIN
3180 LOCATE 1,15:PRINT "Congratulation !!"
3190 REM EFFECT "@53 > A32R8A32R32AR8G8A8B8 > C"
3200 X=X+1:GOTO 3030:GOSUB 4000:REM VSYNC 30:GOTO 3030
4000 REM *VSYNC
4010 FOR LOOP=0 TO 100:NEXT LOOP:RETURN